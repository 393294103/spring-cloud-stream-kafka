buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1") //Send coverage data to coveralls.io
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0') //Build a Docker Image with Gradle
        classpath('org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2') //Integrate with SonarQube
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.palantir.docker'
apply plugin: 'org.sonarqube'

//Docker image name (or tag) is set up from the jar file properties: cristinatech/spring-cloud-stream-kafka
group = 'cristinatech'

bootJar {
    baseName = 'spring-cloud-stream-kafka'
    version = '1.1.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

ext {
    springCloudVersion = 'Finchley.RC1'
}

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-stream-kafka')

    //Production ready features to help you monitor and manage your application
    compile('org.springframework.boot:spring-boot-starter-actuator')

    //Java Persistence API including spring-data-jpa, spring-orm and Hibernate
    compile('org.springframework.boot:spring-boot-starter-data-jpa')

    //Exposing Spring Data repositories over REST via spring-data-rest-webmvc
    compile('org.springframework.boot:spring-boot-starter-data-rest')

    //Instruct Jackson JSON serializer/deserializer to work with Java 8 Date & Time API
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')

    compileOnly('org.projectlombok:lombok:1.16.16') //Gets rid of boiler plate code in Entity, POJOs, Data Objects

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.cloud:spring-cloud-stream-test-support')

    //Browsing Spring Data REST repositories in your browser
    runtime('org.springframework.data:spring-data-rest-hal-browser')

    runtime('org.postgresql:postgresql') //PostgreSQL jdbc driver
    runtime('com.zaxxer:HikariCP') //high-performance, production-quality JDBC connection pool
    runtime('org.flywaydb:flyway-core') //Flyway Database Migrations library

    testRuntime('com.h2database:h2') //H2 in-memory database for tests (with embedded support)
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jacoco {
    toolVersion = "0.8.1"
}

sonarqube {
    properties {
      property "sonar.sources", "src/main/java/"
      property "sonar.projectKey", "cristinatech:spring-cloud-stream-kafka"
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco.exec")
    }
}

jacocoTestReport {
    def coverageSourceDirs = ['src/main/java']
    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }
    sourceDirectories = files(coverageSourceDirs)
}

build.dependsOn jacocoTestReport

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

tasks.coveralls {
    onlyIf { System.env.'CI' }
}

docker {
    dependsOn build
    name "${project.group}/${bootJar.baseName}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}
